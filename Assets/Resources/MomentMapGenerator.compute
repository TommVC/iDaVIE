#pragma kernel MomentZeroGenerator
#pragma kernel MomentOneGenerator
#pragma kernel MaskedMomentZeroGenerator
#pragma kernel MaskedMomentOneGenerator

RWTexture2D<float> MomentResult;
Texture3D<float> DataCube;
Texture3D<int> MaskCube;

float Threshold;
int Depth;

[numthreads(8,8,1)]
void MomentZeroGenerator (uint3 id : SV_DispatchThreadID)
{    
    float sum = 0.0f;
    uint3 index = id.xyz;

    for (int i = 0; i < Depth; i++)
    {
        index.z = i;
        float val = DataCube[index];
        if (val >= Threshold)
        {
            sum += val;           
        }
    }
    MomentResult[id.xy] = sum;    
}

[numthreads(8,8,1)]
void MomentOneGenerator (uint3 id : SV_DispatchThreadID)
{    
    float sum = 0.0f;
    float sumWeighted = 0.0f;
    
    uint3 index = id.xyz;

    for (int i = 0; i < Depth; i++)
    {
        index.z = i;
        float val = DataCube[index];
        if (val >= Threshold)
        {
            sumWeighted += val * i;
            sum += val;
        }
    }
    MomentResult[id.xy] = (sum != 0.0f? sumWeighted / sum: 0.0f);    
}

[numthreads(8,8,1)]
void MaskedMomentZeroGenerator (uint3 id : SV_DispatchThreadID)
{    
    float sum = 0.0f;
    uint3 index = id.xyz;

    for (int i = 0; i < Depth; i++)
    {
        index.z = i;
        int maskVal = MaskCube[index];
        if (maskVal)
        {
            sum += DataCube[index];
        }
    }
    MomentResult[id.xy] = sum;    
}

[numthreads(8,8,1)]
void MaskedMomentOneGenerator (uint3 id : SV_DispatchThreadID)
{    
    float sum = 0.0f;
    float sumWeighted = 0.0f;
    
    uint3 index = id.xyz;

    for (int i = 0; i < Depth; i++)
    {
        index.z = i;
        int maskVal = MaskCube[index];
        if (maskVal)
        {
            float val = DataCube[index];
            sumWeighted += val * i;
            sum += val;
        }
    }
    MomentResult[id.xy] = (sum != 0.0f? sumWeighted / sum: 0.0f);    
}

#pragma kernel LinearColormap

sampler2D ColormapTexture;
RWTexture2D<float4> OutputTexture;

float ClampMin;
float ClampMax;
float ColormapOffset;

[numthreads(8,8,1)]
void LinearColormap (uint3 id : SV_DispatchThreadID)
{
    float inputVal = MomentResult[id.xy];
    float range = ClampMax - ClampMin;
    float val = (inputVal - ClampMin) / range;

    float4 color = tex2Dlod(ColormapTexture, float4(val, 1.0 - ColormapOffset, 0, 0));
    OutputTexture[id.xy] = color;       
} 